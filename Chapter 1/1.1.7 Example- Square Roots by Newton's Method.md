<script type="text/javascript" src="http://cdn.mathjax.org/mathjax/latest/MathJax.js?config=default"></script>

# 1.1.7 示例:牛頓法求平方根 (Example: Square Roots by Newton's Method)

Procedures,as introduced above,are much like ordinary mathematical functions.They specify a value that is determined by one or more parameters.But there is an important difference between mathematical functions and computer procedures.Procedures must be effective.

如上些章節所介紹的程序，與數學中的函數非常相似。它們依據一個或多個參數來指定一個值。但在數學函數和計算機程序間有一個重要的區別。程序是必須有效的(譯者注:可計算完成的)。

As a case in point,consider the problem of computing square roots.We can define the square-root function as

作為一個案例，考慮計算平方根的問題。我們可以如下定義平方根函數

$$\sqrt{r} = the \quad y \quad such \quad that \quad y \geq 0 \quad and \quad y^2 = x$$

This describes a perfectly legitimate mathematical function.We could use it to recognize whether one number is the square root of another,or to derive facts about square roots in general.On the other hand,the definition does not describe a procedure.Indeed,it tells us almost nothing about how to actually find the square root of a given number.It will not help matters to rephrase this definition in pseudo-Lisp:

這描述了一個完美的、合法的數學函數。我們可以用它來識別一個數字是不是另一個數字的平方根，抑或得知一般性的平方根的真相。從另一方面來講，這個定義並沒有描述一個程序。甚至它根本沒有告訴我們如何實際找到一個數字的平方根的任何東西。就算將其翻譯為Lisp偽代碼，它也起不到什麼用處:

	(define (sqrt x)
		(the y (and 	(>= y 0)
						(= (square y) x))))
						
This only begs the question.

這徒增問題。

The contrast between function and procedure is reflection of the general distinction between describing properties of things and describing how to do things,or,as it is sometimes referred to,the distinction between declarative knowledge and imperative knowledge.In mathematical we are usually concerned with declarative(what is)descriptions,whereas in computer science we are usually concerned with imperative(how to)descriptions.<sup>20</sup>

函數和程序之間的差別是描述事物屬性和描述如何做事物的(或者陳述性知識和命令式知識)一般性差別的映照。在數學中我們通常關心的是陳述性描述(是什麼)，然而在計算機科學中我們通常關心的是命令式描述(如何做)。<sup>20</sup>

How does one compute square roots?The most common way is to use Newton's method of successive approximations,which says that whenever we have a guess *y* for the value of the square root of a number *x*,we can perform a simple manipulation to get a better guess(one closer to the actual square root)by averaging *y* with *x/y*.<sup>21</sup> For example,we can compute the square root of 2 as follows.

如何計算平方根呢？最通用的方法是使用牛頓連續近似法，述之為當我們對數字*x*猜測它的平方根為*y*，我們可以通過執行一個簡單的操作(對*y*和*x/y*取平均值)來得到一個更好的猜測數(一個更接近真實平方值的數)。<sup>21</sup>示一例，我們可以如下計算2的平方根。

Suppose our initial guess is 1:

假定我們的初始猜測數為1:

	Guess				Quotient				Average
	1					(2/1)=2				((2+1)/2)=1.5
	1.5					(2/1.5)=1.3333		((1.3333+1.5)/2)=1.4167
	1.4167				(2/1.4167)=1.4118	((1.4167+1.4118)/2)=1.4142
	1.4142				...						...
	
Continuing this process,we obtain better and better approximations to the square root.

持續這一過程，我們得到愈加接近平方根的值。

Now let's formalize the process in terms of procedures.We start with a value for the radicand(the number whose square root we are trying to compute)and a value for the guess.If the guess is good enough for our purposes,we are done;if not,we must repeat the process with an improved guess.We write this basic strategy as a procedure:

現在我們使用程序來標準化這一流程。我們始以一個被開放數(我們試圖計算平方根的數)的值和猜測數的值。如果猜測數已滿足于我們的目的，我們便完成了。如果沒有滿足，我們佐以改進的猜測數重複這一流程。我們把這一基本策略寫為程序:

	(define (sqrt-iter guess x)
		(if (good-enough? guess x)
			 guess
			 (sqrt-iter (improve guess x)
			 				x)))
			 				
A guess is improved by averaging it with the quotient of the radicand and the old guess:

猜測數通過它和舊猜測數與被開方數的商求平均值來得到改進:

	(define (improve guess x)
		(average guess (/ x guess)))
		
where

其中

	(define (average x y)
		(/ (+ x y) 2))
		
We also have to say what we mean by "good enough".The following will do for illustration,but it is not really a very good test.(See exercise 1.7)The idea is to improve the answer until it is close enough so that its square differs from the radicand by less than a predetermined tolerance(here 0.001):<sup>22</sup>

我們亦需明確什麼是“足夠好”。下面會做出示例，但它實際上並不是一個很好的測試。(參看練習1.7)下例的中心思想是不斷改進答案直到足夠接近，也就是猜測數的平方和被開方數之差小於一個預定偏差(這裡是0.001):<sup>22</sup>

	(define (good-enough? guess x)
		(< (abs (- (square guess) x)) 0.001))
		
Finally,we need a way to get started.For instance,we can always guess that the square root of any number is 1:<sup>23</sup>

最終，我們需要一個起步的途徑。比如，我們可以猜測任何數的平方根為1:<sup>23</sup>

	(define (sqrt x)
		(sqrt-iter 1.0 x))
		
If we type these definitions to the interpreter,we can use **sqrt** just as we can use any procedure:

若我們將這些定義輸入到解譯器，我們可以把**sqrt**當做任何程序來使用:

	(sqrt 9)
	3.00009155413138
	(sqrt (+ 100 37))
	11.704699917758145
	(sqrt (+ (sqrt 2) (sqrt 3)))
	1.7739279023207892
	(square (sqrt 1000))
	1000.000369924366
	
The **sqrt** program also illustrates that the simple procedural language we have introduced so far is sufficient for writing any purely numerical program that one could write in,say,C or Pascal.This might seem surprising,since we have not included in our language any iterative(looping)constructs that direct the computer to do something over and over again.**Sqrt-iter**,on the other hand,demonstrates how iteration can be accomplished using no special construct other than the ordinary ability to call a procedure.<sup>24</sup>

**sqrt**程式也展示了目前為止我們介紹的程序語言足以編寫可用C或Pascal編寫的任何純數值型程式。這看起來似乎很奇怪，因為我們並沒有在我們的語言中引入任何迭代型(循環)結構來指示計算機做一遍又一遍重複的事。換個角度來說，**Sqrt-iter**演示了如何只使用程序調用而不用特殊的結構來完成迭代。<sup>24</sup>

## 註解
<sup>24</sup> Readers who are worried about the efficiency issues involved in using procedure calls to implement iteration should note the remarks on "tail recursion" in **Section 1.2.1**.

對於擔心使用程序調用實現迭代會影響效率的讀者，可以參閱**Section 1.2.1**中關於“尾遞歸”的評論。

<script type="text/javascript" src="http://cdn.mathjax.org/mathjax/latest/MathJax.js?config=default"></script>

# 1.2.1 線性遞迴與迭代 (Linear Recursion and Iteration)

(factorial 6)
(* 6 (factorial 5))
(* 6 (* 5 (factorial 4)))
(* 6 (* 5 (* 4 (factorial 3))))
(* 6 (* 5 (* 4 (* 3 (factorial 2)))))
(* 6 (* 5 (* 4 (* 3 (* 2 (factorial 1))))))
(* 6 (* 5 (* 4 (* 3 (* 2 1)))))
(* 6 (* 5 (* 4 (* 3 2))))
(* 6 (* 5 (* 4 6)))
(* 6 (* 5 24))
(* 6 120)
720

首先考慮一個階層函式，定義如下

n! = n * (n - 1) * (n - 2) * ... * 3 * 2 * 1

有很多種方法可以計算階層，我們可以觀察到對於任意正整數n而言，n! 的值等於數字 n 乘上 (n-1)!

n! = n * [(n - 1) * (n - 2) * ... * 3 * 2 * 1]
   = n * (n - 1)!

因此，我們可以用計算(n - 1)!再將其結果乘上n的方式，來計算出n!的值。如果我們規定1!的值等於1，我們可以把上述透過觀察得知的方法寫成一個procedure

(define (factorial n)
  (if (= n 1)
      1
      (* n (factorial (- n 1)))))

我們可以利用 1.1.5 中的置換模型來觀察這個procedure在計算6!時的計算過程，過程如本節一開始所示。

我們現在以另外的觀點來觀察階層函式。我們可以這樣描述階層函式：首先將1和2相乘，然後把結果乘上3，然後乘上4，依此類推直到我們乘到n為止。更正式的來說，我們可以記錄一個持續被使用的乘積(product)，以及一個計數器(counter)從1往上數到n，並讓計數器及乘積同時依照下列規則做計算：

product <- counter * product
counter <- counter + 1

並規定n!的值是當計數器的值達到n時乘績所記錄的值。

(factorial 6)
(fact-iter 1 1 6)
(fact-iter 2 2 6)
(fact-iter 6 3 6)
(fact-iter 24 4 6)
(fact-iter 120 5 6)
(fact-iter 720 6 6)
720

同樣的，我們可以把上述計算階層的方法轉換成procedure:

(define (factorial n)
  (fact-iter 1 1 n))

(define (fact-iter product counter max-count)
  (if (> counter max-count)
      product
      (fact-iter (* counter product)
                 (+ counter 1)
                 max-count)))

跟之前一樣，我們可以用置換模型來觀察這個procedure計算6!的過程，如上所示。

來比較這兩個process，我們首先可以觀察到兩個process幾乎完全沒有不同。兩者都在相同領域中計算相同的數學函式，兩者也都需要與數字n大小成比例的步驟來計算出n!的結果。確實，兩個process都執行了相同的乘法過程，過程中也都獲得了相同的部分乘機結果。然而從另外一個角度來看，當我們考慮兩個process的「形狀」的時候，我們會發現兩者發展的過程截然不同。

我們先來看第一個process，置換模型讓我們看到了一個擴展後收縮的形狀，如圖1.3所示。擴展過程出現於當process建立一連串的延遲操作時(在這個例子裡，指的是一連串的乘法運算)，而收縮過程出現在當先前被延遲的運算確實被執行時。這種有一連串延遲操作的的process被稱作遞迴process(recursive process)，而執行這樣的process需要直譯器持續的紀錄有哪些運算被推遲到稍後執行。在計算n!的過程中，被推遲的乘法運算的數量，以及因此而需要被直譯器記錄下來的運算相關資訊，會隨著n的值而呈線性成長(與n呈正相關)，一如他算出n!的值所需要用的步數需求。這種的process被稱作線性遞迴process(linear recursive process)。

相對的，第二個process並沒有擴展後收縮，在每一步中我們對於任意n值只需要記錄目前的乘積值、計數器值、以及計數器最大上限。這樣的process我們稱做迭代process(iterative process)。概括來說，若一個程序的狀態可以用一個狀態變數記錄下來的，並且依照固定的規則來決定狀態變數怎麼隨著步驟而更動，必要時可以附上狀態變數更動的終止條件，我們便認為這樣的process是一個迭代process。在計算n!的時候，計算答案所需的步驟會隨著n值變大而線性成長，這樣的process我們稱作線性迭代process(linear iterative process)。

我們可以從另一個角度來觀察這兩個process的不同。在迭代中，program中的變數在任何時間都提供了process完整的狀態敘述，如果我們在任何兩個步驟中停止運算，我們只需要提供直譯器所有的狀態變數值，即可繼續先前被中止的運算。遞迴process則是另外一種狀況，以計算階層為例，直譯器中會有一些額外的、「隱藏的」資訊被記錄起來，而這些記錄著「目前process執行到哪」的資訊並不會被存在program的變數中。延遲運算的數量越多，這些需要被記錄起來的資訊也就越多。

比較這兩者時，我們必須很小心的不要把遞迴process跟遞迴procedure的概念搞混。當我們形容一個procedure是遞迴時，我們指的是procedure的定義指得是(不論是直接或間接的)procedure本身的這個事實。但是當我們形容一個process的特性時，以線性遞迴來說，我們指得是process是怎麼演化，而不是他的語句是怎麼寫的。乍看之下或許有點讓人混亂，當我們寫一個像是fact-iter這樣的遞迴procedure，但它實際上是一個迭代process。然而這個process確實是迭代的：他的三個狀態變數都被直譯器以program變數的形式記錄下來，並且只需要記錄這三個變數便可以繼續執行process。

另一個分辨在process和procedure會讓人困惑的理由在於大部分語言實作中(包括Ada、Pascal、C)把遞迴procedure的的直譯過程設計為會隨著procedure呼叫數量變多時消耗更多的記憶體，即使process本身是被敘述為迭代。也因此，這些語言只能利用特殊用途的「迴圈結構」來形容迭代process，例如do、repeat、until、for、while。在Chapter5中，我們需要考慮Scheme並沒有這個缺陷，Scheme在執行迭代process的時候只會用到固定大小的空間，即使迭代process執行的是遞迴procedure。具有這種特性的實作稱作尾遞迴(tail-recursive)，透過這樣的實作我們可以把遞迴表示成一般的procedure呼叫機制，藉此讓上述的特殊迴圈結構只具有作為語法糖的實際用途。